plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group = "io.github.guozheng"
version = "0.0.3"

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.2'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.2'

    // https://mvnrepository.com/artifact/junit/junit
    testImplementation 'junit:junit:4.13.2'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

//    // https://mvnrepository.com/artifact/org.jmockit/jmockit
//    testImplementation 'org.jmockit:jmockit:1.38'
////    // https://mvnrepository.com/artifact/org.jmock/jmock-junit4
//    testImplementation 'org.jmock:jmock-junit4:2.12.0'

    // https://mvnrepository.com/artifact/xerces/xercesImpl
    testImplementation 'xerces:xercesImpl:2.12.2'
}

mainClassName = 'org.woshiadai.jvast.VideoAdProcessor'

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jvast'
            from components.java
            pom {
                name = 'jvast'
                description = 'A Java library for video ad processing, e.g. insert pixels, etc.'
                url = 'https://github.com/guozheng/jvast'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://choosealicense.com/licenses/mit/'
                    }
                }
                developers {
                    developer {
                        id = 'guozheng'
                        name = 'Guozheng Ge'
                        email = 'woshiadai@me.com'
                        organization = "self"
                        organizationUrl = "https://github.com/guozheng"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/guozheng/jvast.git'
                    developerConnection = 'scm:git:ssh://github.com:guozheng/jvast.git'
                    url = 'https://github.com/guozheng/jvast'
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/guozheng/jvast"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }

        // Use this block if you want to use Gradle Publish plugin to publish to OSSRH
        // Set the two environment variables MAVEN_USERNAME and MAVEN_PASSWORD using your OSSRH username and password
//        maven {
//            name = "OSSRH"
//            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
//            credentials {
//                username = System.getenv("MAVEN_USERNAME")
//                password = System.getenv("MAVEN_PASSWORD")
//            }
//        }
    }
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("nexusPublishing") }
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': "${mainClassName}"
    }
    archiveBaseName = "${rootProject.name}"
    duplicatesStrategy = 'include'
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}